//file:noinspection GrUnresolvedAccess
String getCommit() {
    def stdout = new ByteArrayOutputStream()

    exec {
        commandLine "git", "rev-parse", "--short", "HEAD"
        standardOutput = stdout
    }

    return stdout.toString().trim()
}

static boolean isNonStable(String version) {
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?(-jre)?$/
    return !stableKeyword && !(version ==~ regex)
}

plugins {
    id "com.github.ben-manes.versions" version "0.40.0"
}

def releaseVersion = findProperty("github.tag") ?: getCommit()
println("Release version: ${releaseVersion}")

dependencyUpdates {
    configure {
        rejectVersionIf {
            isNonStable(it.candidate.version)
        }

        checkForGradleUpdate = true
    }
}

subprojects {
    apply from: new File(rootDir, "libs.gradle")

    apply plugin: "java"
    apply plugin: "java-library"
    apply plugin: "maven-publish"

    group "io.github.whilein.wcommons"
    version releaseVersion

    repositories {
        mavenCentral()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                pom {
                    name = project.name
                    description = project.description

                    url = "https://github.com/LastCraftInc/wcommons"

                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }

                    developers {
                        developer {
                            id = "whilein"
                            name = "whilein"
                            email = "whilein.1@gmail.com"
                        }
                    }
                }

                groupId = project.group
                artifactId = project.name
                version = project.version

                from components.java
            }
        }

        repositories {
            mavenLocal()

            // github packages
            maven {
                url = "https://maven.pkg.github.com/LastCraftInc/wcommons"

                credentials {
                    username = findProperty("gpr.user")
                    password = findProperty("gpr.password")
                }
            }
        }
    }

    dependencies {
        compileOnly libs.lombok
        annotationProcessor libs.lombok

        testCompileOnly libs.lombok
        testAnnotationProcessor libs.lombok

        compileOnly libs.annotations
        testCompileOnly libs.annotations

        testImplementation libs.junit.api
        testRuntimeOnly libs.junit.engine
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        targetCompatibility = JavaVersion.VERSION_11
        sourceCompatibility = JavaVersion.VERSION_11
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    test {
        useJUnitPlatform()

        testLogging {
            showStandardStreams = true

            exceptionFormat "short"
            events("started", "skipped", "failed")
        }
    }
}